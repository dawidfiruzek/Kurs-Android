apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-kapt'

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath ClassPaths.fabric
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion Versions.compileSdk
    defaultConfig {
        multiDexEnabled true
        applicationId "pl.dawidfiruzek.kursandroid"
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode Versions.versionCode
        versionName Versions.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "mode"
    productFlavors {
        develop {
            dimension "mode"
            applicationIdSuffix ".develop"
            versionNameSuffix "-develop"
        }
        full {
            dimension "mode"
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    applicationVariants.all {
        buildConfigField 'String', 'BASE_URL', KURS_APP_BASE_URL
        buildConfigField 'String', 'AMPLITUDE_KEY', KURS_APP_AMPLITUDE_KEY
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation Libs.kotlinStdlib
    implementation Libs.supportAppcompat
    implementation Libs.supportRecycler
    implementation Libs.supportDesign
    implementation(Libs.crashlytics) {
        transitive = true
    }
    implementation Libs.timber
    implementation Libs.butterknife
    kapt Libs.butterknifeCompiler
    implementation Libs.dagger2
    kapt Libs.dagger2Compiler
    implementation Libs.dagger2Android
    kapt Libs.dagger2AndroidCompiler
    implementation Libs.hawk
    implementation Libs.rxAndroid
    implementation Libs.rxJava
    implementation Libs.parceler
    kapt Libs.parcelerCompiler
    implementation Libs.lottie
    implementation Libs.rxPremissions
    implementation Libs.rxBinding
    implementation Libs.retrofit
    implementation Libs.retrofitGsonConverter
    implementation Libs.retrofitRxAdapter
    implementation Libs.multiDex
    implementation Libs.glide
    kapt Libs.glideCompiler
    implementation Libs.amplitude
    debugImplementation Libs.leakCanary
    debugImplementation Libs.chuck
    debugImplementation Libs.stetho
    debugImplementation Libs.stethoNetwork
    testImplementation Libs.junit
    testImplementation Libs.mockito
    androidTestImplementation Libs.testRunner
    androidTestImplementation Libs.espressoCore
}
